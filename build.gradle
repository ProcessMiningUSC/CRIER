plugins {
    id 'org.jetbrains.kotlin.jvm'       version '1.5.10' apply false
    id 'org.jmailen.kotlinter'          version '3.3.0'  apply false
}

allprojects {
    group   'gal.usc.citius.processmining'
    version '5.1.3'

    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jmailen.kotlinter'

    compileKotlin {
        kotlinOptions.jvmTarget = "11"
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = "11"
    }

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(11))
        }
        withSourcesJar()
    }

    test {
        useJUnitPlatform {
            includeEngines 'spek2'
        }
    }

    repositories {
        mavenCentral()
        mavenLocal()

        maven {
            url "https://gitlab.citius.gal/api/v4/projects/1048/packages/maven"
            name "ProM"
        }

        maven {
            url "https://gitlab.citius.gal/api/v4/projects/1987/packages/maven"
            name "Libs"
        }

        maven {
            url "https://gitlab.citius.gal/api/v4/projects/1403/packages/maven"
            name "Project"
        }

        maven {
            url "https://gitlab.citius.gal/api/v4/projects/1126/packages/maven"
            name "Custom NE Precision"
        }
    }
}

subprojects {
    ext {
        commonCSVVersion         = '1.8'
        kotlinCoroutinesVersion  = '1.5.0'
        smileVersion             = '2.5.3'
        doverVersion             = '1.0'
        commonsMathVersion       = '3.6.1'
        cobefraVersion           = '2018.04.03-CLEAN'
        promVersion              = '6.7'
        customNEPrecisionVersion = '1.0'
        openXESVersion           = '2.16'
        troveVersion             = '3.0.3'
        coltVersion              = '1.2.0'
        graphvizVersion          = '0.18.1'
        guavaVersion             = '30.1-jre'
        dom4jVersion             = '2.1.3'
        hipsterVersion           = '1.0.1'
        komaVersion              = '0.12.1'
        jaxbVersion              = '2.3.1'
        kotlinStatistics         = '1.2.1'
        letsplot                 = '2.2.1'
        letsplotKotlin           = '3.1.1'
    }

    dependencies {
        implementation      group: "org.jetbrains.kotlin",      name: "kotlin-stdlib-jdk8"
        implementation      group: 'io.github.microutils',      name: 'kotlin-logging-jvm',     version: '2.0.4'

        testImplementation  group: 'junit',                     name: 'junit',                  version: '4.13'
        testImplementation  group: 'org.junit.platform',        name: 'junit-platform-runner',  version: '1.6.0'
        testImplementation( group: 'org.amshove.kluent',        name: 'kluent',                 version: '1.64') {
            exclude         group: 'org.jetbrains.kotlin',      module: 'kotlin-reflect'
        }
        testImplementation( group: 'org.spekframework.spek2',   name: 'spek-dsl-jvm',           version: '2.0.9') {
            exclude         group: 'org.jetbrains.kotlin'
        }
        testRuntimeOnly(    group: 'org.spekframework.spek2',   name: 'spek-runner-junit5',     version: '2.0.9') {
            exclude         group: 'org.junit.platform'
            exclude         group: 'org.jetbrains.kotlin'
        }
        testRuntimeOnly(    group: 'org.jetbrains.kotlin',      name: 'kotlin-reflect',         version: '1.5.10')
    }
}

wrapper {
    gradleVersion = "7.4.2"
}
